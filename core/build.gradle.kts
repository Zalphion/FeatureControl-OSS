plugins {
    alias(libs.plugins.ksp)
}

dependencies {
    api(libs.http4k.core)
    api(libs.http4k.format.core)
    api(libs.service.utils)
    api(libs.kotlin.logging.jvm)
    api(libs.forkhandles.values4k)
    api(libs.forkhandles.result4k)
    api(libs.kotshi.api)
    api(libs.kotlinx.html)

    implementation(libs.nimbus.jose.jwt)
    implementation(libs.http4k.format.moshi) {
        exclude(group = "org.jetbrains.kotlin", module = "kotlin-reflect")
    }

    ksp(libs.kotshi.compiler)

    testFixturesApi(libs.junit.jupiter.api)
    testFixturesApi(libs.forkhandles.result4k.kotest)
    testFixturesApi(libs.http4k.testing.playwright)
    testFixturesApi(libs.kotest.assertions.core.jvm)

    // include webjars for ui tests
    testFixturesRuntimeOnly(libs.webjars.uikit)
    testFixturesRuntimeOnly(libs.webjars.alpinejs)
    testFixturesRuntimeOnly(libs.webjars.dayjs)

    testFixturesRuntimeOnly(libs.tinylog.slf4j)
    testFixturesRuntimeOnly(libs.tinylog.impl)
    testFixturesRuntimeOnly(libs.junit.jupiter)
    testFixturesRuntimeOnly(libs.junit.platform.launcher)
}

val generateWebAssetVersions: TaskProvider<Task> = tasks.register("generateWebAssetVersions") {
    val outputDir = layout.buildDirectory.dir("generated/sources/webAssetVersions/kotlin")
    outputs.dir(outputDir)

    val uikit = libs.versions.uikit.get()
    val alpinejs = libs.versions.alpinejs.get()
    val dayjs = libs.versions.dayjs.get()

    doLast {
        val out = outputDir.get().asFile
        val pkgDir = out.resolve("com/zalphion/featurecontrol/web")
        pkgDir.mkdirs()

        pkgDir.resolve("WebAssetVersions.kt").writeText(
            """
            package com.zalphion.featurecontrol.web

            /**
             * Generated by Gradle from libs.versions.toml
             */
            internal object WebAssetVersions {
                const val UI_KIT = "$uikit"
                const val ALPINE_JS = "$alpinejs"
                const val DAY_JS = "$dayjs"
            }
            """.trimIndent()
        )
    }
}

configure<SourceSetContainer> {
    getByName("main").java.srcDir(layout.buildDirectory.dir("generated/sources/webAssetVersions/kotlin"))
}

tasks.named("compileKotlin") {
    dependsOn(generateWebAssetVersions)
}

// Wait until the KSP plugin is applied (and tasks are created), then wire dependency.
plugins.withId("com.google.devtools.ksp") {
    tasks.matching { it.name == "kspKotlin" }.configureEach {
        dependsOn(generateWebAssetVersions)
    }
}